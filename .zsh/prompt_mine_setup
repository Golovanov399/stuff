# mine prompt theme

prompt_mine_setup () {
  # Some can't be local
  local prompt_gfx_tlc prompt_gfx_mlc prompt_gfx_blc

  if [[ $1 == '8bit' ]]; then
    shift
    if [[ $(echo -n ${LC_ALL:-${LC_CTYPE:-$LANG}} | tr '[:lower:]' '[:upper:]') = *UTF*8* ]]; then
      prompt_gfx_tlc=$'\xe2\x94\x8c'
      prompt_gfx_mlc=$'\xe2\x94\x9c'
      prompt_gfx_blc=$'\xe2\x94\x94'
      prompt_gfx_hyphen=$'\xe2\x94\x80'
    else
      prompt_gfx_tlc=$'\xda'
      prompt_gfx_mlc=$'\xc3'
      prompt_gfx_blc=$'\xc0'
      prompt_gfx_hyphen=$'\xc4'
    fi
  else
    prompt_gfx_tlc='.'
    prompt_gfx_mlc='|'
    prompt_gfx_blc='\`'
    prompt_gfx_hyphen='-'
  fi

  # Colour scheme
  prompt_mine_color1=${1:-'cyan'}    # hyphens
  prompt_mine_color2=${2:-'green'}   # current directory
  prompt_mine_color3=${3:-'cyan'}    # user@host
  prompt_mine_color4=${4:-'white'}   # user input

  local prompt_gfx_bbox 
  prompt_gfx_tbox="%b%F{$prompt_mine_color1}${prompt_gfx_tlc}%b%F{$prompt_mine_color1}${prompt_gfx_hyphen}"
  prompt_gfx_bbox="%b%F{$prompt_mine_color1}${prompt_gfx_blc}${prompt_gfx_hyphen}%b%F{$prompt_mine_color1}"

  # This is a cute hack.  Well I like it, anyway.
  prompt_gfx_bbox_to_mbox=$'%{\e[A\r'"%}%B%F{$prompt_mine_color1}${prompt_gfx_mlc}%b%F{$prompt_mine_color1}${prompt_gfx_hyphen}%{"$'\e[B%}'

  prompt_l_paren="%B%F{black}("
  prompt_r_paren="%B%F{black})"
  
  prompt_user_host="%b%F{$prompt_mine_color3}%n%B%F{$prompt_mine_color3}@%b%F{$prompt_mine_color3}%m"

  prompt_line_2="$prompt_gfx_bbox${prompt_gfx_hyphen}%B%F{white}"

  prompt_char="%(!.#.>)"

  prompt_opts=(cr subst percent)
    
  add-zsh-hook precmd prompt_mine_precmd
}

prompt_mine_precmd() {
  setopt noxtrace localoptions extendedglob
  
  # I want to know my battery percentage when running on battery power
  if which acpi &> /dev/null; then
    local battstate="$(acpi -b)"
    local battprcnt="$(echo ${battstate[(w)4]}|sed -r 's/(^[0-9]+).*/\1/')"
    if [[ -z "${battprcnt}" ]]; then
        prompt_battery=""
    elif [[ "${battprcnt}" -lt 15 ]]; then
        prompt_battery="batt:%b%F{red}${battprcnt}%%"
    elif [[ "${battprcnt}" -lt 50 ]]; then
        prompt_battery="batt:%b%F{yellow}${battprcnt}%%"
    else 
        prompt_battery="batt:%b%F{green}${battprcnt}%%"
    fi
  fi
  #I also want to know some stuff about VCS
  vcs_info
  prompt_vcs=""
  if [ ! -z ${vcs_info_msg_0_} ]; then
	  prompt_vcs=" ${vcs_info_msg_0_} "
  fi
  
  prompt_line_1a="$prompt_gfx_tbox$prompt_l_paren%B%F{$prompt_mine_color2}%~$prompt_r_paren%b${prompt_vcs}%F{$prompt_mine_color1}"
  prompt_line_1b=" $prompt_l_paren$prompt_battery$prompt_r_paren $prompt_l_paren$prompt_user_host$prompt_r_paren $prompt_l_paren%b%F{${prompt_mine_color2}}%T$prompt_r_paren %b%F{$prompt_mine_color1}${prompt_gfx_hyphen}"


  
  local prompt_line_1

  prompt_mine_choose_prompt

  PS1="$prompt_line_1$prompt_newline$prompt_line_2%B%F{grey}$prompt_char %b%f%k"
  PS2="$prompt_line_2$prompt_gfx_bbox_to_mbox%B%F{grey}%_> %b%f%k"
  PS3="$prompt_line_2$prompt_gfx_bbox_to_mbox%B%F{grey}?# %b%f%k"
  zle_highlight[(r)default:*]="default:fg=$prompt_mine_color4"
}

prompt_mine_choose_prompt () {
  local prompt_line_1a_width=${#${(S%%)prompt_line_1a//(\%([KF1]|)\{*\}|\%[Bbkf])}}
  local prompt_line_1b_width=${#${(S%%)prompt_line_1b//(\%([KF1]|)\{*\}|\%[Bbkf])}}

  local prompt_padding_size=$(( COLUMNS
                                  - prompt_line_1a_width
                                  - prompt_line_1b_width ))

  # Try to fit in long path and user@host.
  if (( prompt_padding_size > 0 )); then
    local prompt_padding
    eval "prompt_padding=\${(l:${prompt_padding_size}::${prompt_gfx_hyphen}:)_empty_zz}"
    prompt_line_1="$prompt_line_1a$prompt_padding$prompt_line_1b"
    return
  fi

  prompt_padding_size=$(( COLUMNS - prompt_line_1a_width ))

  # Didn't fit; try to fit in just long path.
  if (( prompt_padding_size > 0 )); then
    local prompt_padding
    eval "prompt_padding=\${(l:${prompt_padding_size}::${prompt_gfx_hyphen}:)_empty_zz}"
    prompt_line_1="$prompt_line_1a$prompt_padding"
    return
  fi

  # Still didn't fit; truncate 
  local prompt_pwd_size=$(( COLUMNS - 5 ))
  prompt_line_1="$prompt_gfx_tbox$prompt_l_paren%B%F{$prompt_mine_color2}%$prompt_pwd_size<...<%~%<<$prompt_r_paren%b%F{$prompt_mine_color1}$prompt_gfx_hyphen"
}

prompt_mine_setup "$@"
